plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.97'
    id 'idea'

}


version = mod_version
group = mod_group_id

repositories {
    mavenLocal()
    mavenCentral()
    maven { // Modrinth File, Jade
        name = "Modrinth Maven"
        url = "https://api.modrinth.com/maven"
    }
    maven { // AnvilCraft
        name = "Cjsah Maven"
        url = "https://server.cjsah.net:1002/maven/"
    }
    maven { // Registrate
        name = "Devos Maven"
        url = "https://mvn.devos.one/snapshots"
    }
    maven { // Curios API
        name = "OctoStudios Maven"
        url = uri("https://maven.octo-studios.com/releases")
    }
    maven { // JEI
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven { // Create
        name = "Create Maven"
        url = "https://maven.createmod.net"
    }
}

base {
    archivesName = mod_id
}

// Mojang ships Java 21 to end users starting in 1.20.5, so mods should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    // Specify the version of NeoForge to use.
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }


    runs {
        var isJbr = System.getProperty('java.vm.vendor').contains('JetBrains')

        client {
            client()
            if (isJbr) {
                jvmArgument "-XX:+AllowEnhancedClassRedefinition"
            }
            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            if (isJbr) {
                jvmArgument "-XX:+AllowEnhancedClassRedefinition"
            }
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()

            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    // AnvilCraft
    implementation("dev.dubhe:anvilcraft-neoforge-1.21.1:${anvilcraft_version}"){
        transitive = false
    }
    // Anvillib
    implementation(libs.anvillib)
    interfaceInjectionData(libs.anvillib)
    // JEI
    compileOnlyApi("mezz.jei:jei-1.21.1-common-api:${jei_version}") { transitive = false }
    compileOnlyApi("mezz.jei:jei-1.21.1-neoforge-api:${jei_version}") { transitive = false }
    implementation("mezz.jei:jei-1.21.1-neoforge:${jei_version}") { transitive = false }
    // Registrate
    api("com.tterrag.registrate:Registrate:${registrate_version}")
    // JEI
    compileOnlyApi("mezz.jei:jei-1.21.1-common-api:${jei_version}") { transitive = false }
    compileOnlyApi("mezz.jei:jei-1.21.1-neoforge-api:${jei_version}") { transitive = false }
    implementation("mezz.jei:jei-1.21.1-neoforge:${jei_version}") { transitive = false }
    // For more info:
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'

}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}
// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets.main.resources.srcDir generateModMetadata
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
neoForge.ideSyncTask generateModMetadata

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
